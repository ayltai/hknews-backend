plugins {
    id 'org.springframework.boot'        version '2.1.7.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    id 'checkstyle'
    id 'com.github.spotbugs'             version '2.0.0'
    id 'jacoco'
    id 'org.owasp.dependencycheck'       version '5.2.1'
    id 'com.github.ben-manes.versions'   version '0.22.0'
}

group   = 'com.github.ayltai'
version = '1.0.0'

sourceCompatibility = 11
targetCompatibility = 11

configurations {
    codacy

    compileOnly {
        extendsFrom annotationProcessor
    }
}

//region Dependencies

ext {
    springVersion        = '2.1.7.RELEASE'
    okhttpVersion        = '4.1.0'
    retrofitVersion      = '2.6.1'
    lombokVersion        = '1.18.8'
    embeddedMongoVersion = '2.2.0'
}

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://dl.bintray.com/mockito/maven' }
    maven { url 'https://dl.bintray.com/typesafe/maven-releases' }
}

dependencies {
    // Spring Boot
    implementation ("org.springframework.boot:spring-boot-starter-web:$springVersion") {
        exclude group : 'org.springframework.boot', module : 'spring-boot-starter-json'
        exclude group : 'org.springframework.boot', module : 'spring-boot-starter-tomcat'
        exclude group : 'org.hibernate.validator',  module : 'hibernate-validator'
    }
    implementation      "org.springframework.boot:spring-boot-starter-undertow:$springVersion"
    implementation      "org.springframework.boot:spring-boot-starter-data-mongodb:$springVersion"
    implementation      "org.springframework.boot:spring-boot-starter-actuator:$springVersion"
    implementation      'io.micrometer:micrometer-registry-prometheus:1.2.0'
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:$springVersion"

    // HTTP networking
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-simplexml:$retrofitVersion"

    // Utilities
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'org.json:json:20190722'

    // Code generation
    implementation      'org.aspectj:aspectjweaver:1.9.4'
    compileOnly         "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    // Unit testing
    testImplementation "org.springframework.boot:spring-boot-starter-test:$springVersion"
    testImplementation "de.flapdoodle.embed:de.flapdoodle.embed.mongo:$embeddedMongoVersion"

    codacy 'com.codacy:codacy-coverage-reporter:6.0.2'
}

//endregion

//region Checkstyle configurations

checkstyle {
    toolVersion    '8.23'
    configFile     rootProject.file('./checkstyle.xml')
    ignoreFailures true
    showViolations true
}

tasks.withType(Checkstyle) {
    source  'src/main/java'
    include '**/*.java'
    exclude '**/*Test.java'

    reports {
        xml.enabled      = true == System.getenv('CI')
        html.enabled     = true == !System.getenv('CI')
        xml.destination  file("$project.buildDir/reports/checkstyle/checkstyle-output.xml")
        html.destination file("$project.buildDir/reports/checkstyle/checkstyle-output.html")
    }
}

//endregion

//region SpotBugs configurations

spotbugs {
    toolVersion    '3.1.12'
    effort         'max'
    reportLevel    'medium'
    ignoreFailures true
    excludeFilter  rootProject.file('./spotbugs-exclude.xml')
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled  = true == System.getenv('CI')
        html.enabled = true == !System.getenv('CI')
    }
}

//endregion

//region JaCoCo configurations

jacoco {
    toolVersion '0.8.4'
}

jacocoTestReport {
    dependsOn 'test'

    reports {
        xml.enabled  = true
        html.enabled = true
    }
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes                 = [ 'jdk.internal.*' ]
}

//endregion

//region OWASP dependency check configurations

check.dependsOn dependencyCheckAnalyze

dependencyCheck {
    suppressionFile = 'suppressions.xml'
}

//endregion

//region Codacy configurations

task uploadCoverageToCodacy(type : JavaExec, dependsOn : jacocoTestReport) {
    main      = 'com.codacy.CodacyCoverageReporter'
    classpath = configurations.codacy

    args = [
        'report',
        '-l',
        'Java',
        '-r',
        "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    ]
}

task (codacyDepsize) {
    doLast {
        def size = 0

        configurations.codacy.collect {
            it.length() / (1024 * 1024)
        }.each {
            size += it
        }

        println "Total dependencies size: ${Math.round(size * 100) / 100} MB"

        configurations.codacy.sort {
            -it.length()
        }.each {
            println "${it.name}: ${Math.round(it.length() / (1024) * 100) / 100} KB"
        }
    }
}

task (codacyLocs) {
    doLast {
        configurations.codacy.each {
            String jarName = it

            println jarName
        }
    }
}

//endregion
